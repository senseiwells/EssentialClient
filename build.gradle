plugins {
	id "maven-publish"
	id "fabric-loom" version "1.4-SNAPSHOT"
	id 'me.modmuss50.mod-publish-plugin' version '0.3.4'
}

repositories {
	mavenCentral()
	maven {
		url "https://masa.dy.fi/maven"
	}
	maven {
		url "https://maven.terraformersmc.com/releases/"
	}
	maven {
		url "https://jitpack.io"
	}
	maven {
		url "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1"
	}
	maven {
		url 'https://www.cursemaven.com'
	}
}

version = project.minecraft_version + "-" + project.mod_version
group = project.maven_group

//configurations.configureEach {
//	resolutionStrategy {
//		force("net.fabricmc:fabric-loader:$loader_version")
//	}
//}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings "net.fabricmc:yarn:${yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${loader_version}"

	modImplementation "com.github.gnembon:fabric-carpet:${carpet_version}"
	modImplementation "com.terraformersmc:modmenu:${modmenu_version}"

	// Required for Arucas
	include implementation("org.jetbrains.kotlin:kotlin-stdlib:1.7.10")
	var arucas = arucas_version.length() > 10 ? arucas_version.substring(0, 10) : arucas_version
	include implementation("com.github.senseiwells:Arucas:${arucas}")
	include implementation("net.bytebuddy:byte-buddy:${bytebuddy_version}")

	// Mixin Extras
	//include implementation(annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-fabric:${mixin_extras_version}"))

	// Runtime Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

	// Account authentication at runtime
	// modRuntimeOnly "me.djtheredstoner:DevAuth-fabric:1.0.0"
}

processResources {
	inputs.property "version", mod_version

	filesMatching("fabric.mod.json") {
		def map = [
			"version"             : mod_version,
			"carpet_dependency"   : ">=" + carpet_version,
			"minecraft_dependency": minecraft_dependency
		]
		expand map
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
	it.options.release.set(16)
}

java {
	withSourcesJar()
}

jar {
	from "LICENSE"
}

publishMods {
	file = remapJar.archiveFile
	changelog = """
	- Fixed a conflict with ViaFabricPlus
	- Fixed some obfuscation issues with clientscript
	"""
	type = STABLE
	modLoaders.add("fabric")

	displayName = "EssentialClient ${minecraft_version} v${mod_version}"

	modrinth {
		accessToken = providers.environmentVariable("MODRINTH_API_KEY")
		projectId = "sH0dfrKf"
		minecraftVersions.add(minecraft_version)

		requires {
			id = "P7dR8mSH"
		}
		requires {
			id = "TQTTVgYE"
		}
		optional {
			id = "mOgUt4GM"
		}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(jar) {
				builtBy remapJar
			}
		}
	}
}