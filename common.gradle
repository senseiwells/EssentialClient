plugins {
	id "maven-publish"
	id "fabric-loom" version "1.0-SNAPSHOT"
	id "com.github.johnrengelman.shadow" version "7.1.2"
	id "io.github.juuxel.loom-quiltflower" version "1.7.0"
	id "com.replaymod.preprocess" version "897c4096e2"
}

int mcVersion = 1
preprocess {
	mcVersion = vars.get()["MC"]
	tabIndentation = true
}

repositories {
	mavenCentral()
	maven {
		url "https://masa.dy.fi/maven"
	}
	maven {
		url "https://maven.terraformersmc.com/releases/"
	}
	maven {
		url "https://jitpack.io"
	}
	maven {
		url "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1"
	}
	maven {
		url 'https://www.cursemaven.com'
	}
}

configurations {
	shade
}

archivesBaseName = project.archives_base_name
version = project.minecraft_version + "-" + project.mod_version
group = project.maven_group

shadowJar {
	configurations = [project.configurations.shade]
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "carpet:fabric-carpet:${project.carpet_core_version}"
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

	// Required for Arucas
	include implementation("org.jetbrains.kotlin:kotlin-stdlib:1.7.10")
	include implementation("com.github.senseiwells:Arucas:${project.arucas_version.length() > 10 ? project.arucas_version.substring(0, 10) : project.arucas_version}")

	// include "net.dv8tion:JDA:5.0.0-alpha.19"
	// include implementation("com.github.senseiwells:ArucasDiscordAPI:${project.arucas_discord_api_version.length() > 10 ? project.arucas_discord_api_version.substring(0, 10) : project.arucas_discord_api_version}")

	// Multiconnect API
	include modImplementation("net.earthcomputer.multiconnect:multiconnect-api:${project.multiconnect_api_version}") {
		transitive = false
	}

	// Mixin Extras
	shade implementation("com.github.LlamaLad7:MixinExtras:0.0.12")
	annotationProcessor "com.github.LlamaLad7:MixinExtras:0.0.12"

	// Runtime Fabric API
	modImplementation fabricApi.module("fabric-textures-v0", project.fabric_version)
	modImplementation fabricApi.module("fabric-rendering-fluids-v1", project.fabric_version)

	// Account authentication at runtime
	modRuntimeOnly "me.djtheredstoner:DevAuth-fabric:1.0.0"

	compileOnly "com.google.code.findbugs:jsr305:3.0.2"
}

processResources {
	inputs.property "version", project.mod_version

	filesMatching("fabric.mod.json") {
		def map = [
			"version"             : project.mod_version,
			"carpet_dependency"   : project.carpet_dependency,
			"minecraft_dependency": project.minecraft_dependency
		]
		expand map
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
	it.options.release = 16
}

java {
	withSourcesJar()
}

prepareRemapJar {
	dependsOn "shadowJar"
}

remapJar {
	remapperIsolation = true
	inputFile.set shadowJar.archiveFile.get()
	doLast {
		//shadowJar.archiveFile.get().asFile.delete()
	}
}

shadowJar {
	from "LICENSE"

	relocate("com.llamalad7", "shadow.llamalad7")
}


// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(jar) {
				builtBy remapJar
			}
		}
	}
}