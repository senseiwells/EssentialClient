pluginManagement {
	repositories {
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		maven {
			name = 'Cotton'
			url = 'https://server.bbkr.space/artifactory/libs-release/'
		}
		maven {
			name = 'Jitpack'
			url = "https://jitpack.io"
		}
		maven {
			url "https://masa.dy.fi/maven"
		}
		mavenCentral()
		gradlePluginPortal()
	}
	resolutionStrategy {
		eachPlugin {
			switch (requested.id.id) {
				case "com.replaymod.preprocess": {
					useModule("com.github.Fallen-Breath:preprocessor:${requested.version}")
				}
			}
		}
	}
}
def versions = Arrays.asList(
	"1.17.1",
	"1.18.2",
	"1.19.3"
)

for (String version : versions) {
	include(":$version")

	def proj = project(":$version")
	proj.projectDir = file("versions/$version")
	proj.buildFileName = "../../common.gradle"
}
